#!/usr/bin/perl

use strict;
use warnings;
use v5.6.1;
use IO::Select;
use IO::Socket;
use Socket;

my $version = 'Veachian-0.05';
my $network = 'V64net';
my $server  = 'G42.V64.net';
my @days = qw/ Sun Mon Tue Wed Thu Fri Sat /;
my @months = qw/ Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec /;
my $serv_startup = sprintf("%s %s %02d %04d at %02d:%02d:%02d %s", $days[(localtime)[6]], $months[(localtime)[4]],
                                                                   (localtime)[3], (localtime)[5] + 1900, (localtime)[2],
                                                                   (localtime)[1], (localtime)[0], (localtime)[8] ? 'CDT' : 'CST');
my $motd = 'vircd.motd';
my $port = '4242';

my %users = ();
$users{local}{users} = 0;
$users{global}{users} = 0;

$users{local}{record_users} = 0;
$users{global}{record_users} = 0;

$users{local}{invisible_users} = 0;
$users{global}{invisible_users} = 0;

$users{local}{non_invisible_users} = 0;
$users{global}{non_invisible_users} = 0;

$users{local}{servers} = 0;
$users{global}{servers} = 1;

$users{local}{operators} = 0;
$users{global}{operators} = 0;

$users{local}{channels} = 0;
$users{global}{channels} = 0;

my @clients_to_disconnect = ();

my $serv_listen;
my $serv_readable;
my $serv_writable;

print "Starting $version...\n";

$serv_listen = IO::Socket::INET->new(LocalPort => $port,
                                     Listen    => 10,
                                     Proto     => 'tcp',
                                     Reuse     => 1)
or die "Unable to creating listening socket.\n";

$serv_readable = IO::Select->new();
$serv_readable->add($serv_listen);
$serv_writable = IO::Select->new();
$serv_writable->add($serv_listen);

print "Server running.\n";

run_server();
exit;

sub run_server {
    while (1) {
        my @ready = IO::Select->select($serv_readable, $serv_writable, undef, 0.1);

        foreach my $write_fh (@{$ready[1]}) {
            my $write = syswrite($write_fh, $users{$write_fh}{recv_buf}, 3072);
            if (!$write) { warn "Error sending data to $users{$write_fh}{nick}: $!\nData will be lost.\n" }
            $users{$write_fh}{recv_buf} = '';
            $serv_writable->remove($write_fh);
        }

        foreach my $fh (@{$ready[0]}) {
            if ($fh == $serv_listen) {
                process_login($fh);
            }

            else {                
                my $read = sysread($fh, $users{$fh}{sent_buf}, 3072);
                if ($read) {
                    my @bufs = split(/\n/, $users{$fh}{sent_buf});
                    $users{$fh}{sent_buf} = '';
                    foreach my $args (@bufs) {
                        print "Received from $users{$fh}{real_host_name}: $args\n";
                        process_command($fh, $args);
                    }
                }
                else { push @clients_to_disconnect, $fh }
            }
        }

        if ($clients_to_disconnect[0]) { disconnect_dead_clients() }
    }
}

sub look_up_host {
    my ($fh) = @_;
    my $other_end = getpeername($fh);
    my $iaddr = (unpack_sockaddr_in($other_end))[1];
    my $actual_ip = inet_ntoa($iaddr);
    my $claimed_hostname = gethostbyaddr($iaddr, AF_INET);
    my $name_lookup = (gethostbyname($claimed_hostname))[0];
    if (!$name_lookup) {
        print "Client connected: $actual_ip\n";
        my (@values) = (0, $actual_ip);
        return @values;
    }
    print "Client connected: $name_lookup\n";
    my (@values) = (1, $name_lookup);
    @values;
}

sub send_user_msg_addr {
    my ($fh, $output) = @_;
    my $message = ":"."$users{$fh}{nick}"."!"."$users{$fh}{user_name}"."@"."$users{$fh}{real_host_name} ";
    $message .= $output . "\n";
    $users{$fh}{recv_buf} .= $message;
    $serv_writable->add($fh); 
}

sub send_user_msg_serv {
    my ($fh, $output) = @_;
    my $message = ":$server ";
    $message .= $output . "\n";
    $users{$fh}{recv_buf} .= $message;
    $serv_writable->add($fh);
}

sub send_user_msg_raw {
    my ($fh, $output) = @_;
    my $message .= $output . "\n";
    $users{$fh}{recv_buf} .= $message;
    $serv_writable->add($fh);
}

sub send_user_msg_num {
    my ($fh, $numeric, $output) = @_;
    my $message;
    if ($users{$fh}{nick}) { $message = ":$server $numeric $users{$fh}{nick} " }
    else { $message = ":$server $numeric * " }
    $message .= $output . "\n";
    $users{$fh}{recv_buf} .= $message;
    $serv_writable->add($fh);
}

sub process_login {
    my $fh = $serv_listen->accept;
    $serv_readable->add($fh);
    send_user_msg_serv($fh, "NOTICE AUTH :*** Looking up your hostname...");
    my $value;
    ($value, $users{$fh}{real_host_name}) = look_up_host($fh);
    if (!$value) { send_user_msg_serv($fh, "NOTICE AUTH :*** Hostname not resolved. Using IP instead: ($users{$fh}{hostname})") }
    else { send_user_msg_serv($fh, "NOTICE AUTH :*** Hostname found: $users{$fh}{real_host_name}") }
    $users{$fh}{connected} = 0;
}

sub new_logon {
    my ($fh) = @_;
    $users{local}{users}++;
    $users{local}{record_users} = max($users{local}{users}, $users{local}{record_users});
    $users{local}{non_invisible_users}++;
    calc_global_nums();
    send_user_msg_num($fh, '001', ":Welcome to the $network IRC Network $users{$fh}{nick}".'!'."$users{$fh}{user_name}".'@'."$users{$fh}{real_host_name}");
    send_user_msg_num($fh, '002', ":Your host is $server, running version $version");
    send_user_msg_num($fh, '003', ":This server was created $serv_startup");
    send_user_msg_num($fh, '251', ":There are $users{global}{non_invisible_users} users and $users{global}{invisible_users} invisible on $users{global}{servers} servers");
    send_user_msg_num($fh, '252', "$users{global}{operators} :operator(s) online");
    send_user_msg_num($fh, '254', "$users{global}{channels} :channels formed");
    send_user_msg_num($fh, '255', ":I have $users{local}{users} clients and $users{local}{servers} servers");
    send_user_msg_num($fh, '265', ":Current Local Users: $users{local}{users}  Max: $users{local}{record_users}");
    send_user_msg_num($fh, '266', ":Current Global Users: $users{global}{users}  Max: $users{global}{record_users}");
    send_user_msg_num($fh, '375', ":- $server Message of the Day -");
    open(MOTD, "<vircd.motd") or warn "Error opening MOTD: $!\n";
    while (<MOTD>) { send_user_msg_num($fh, '372', ":- $_") }
    close MOTD;
    send_user_msg_num($fh, '376', ":End of /MOTD command.");
    
}

sub max { ($_[0] > $_[1]) ? $_[0] : $_[1] }
sub min { ($_[0] < $_[1]) ? $_[0] : $_[1] }

sub calc_global_nums() {
    $users{global}{users}               = $users{local}{users};
    $users{global}{record_users}        = $users{local}{record_users};
    $users{global}{invisible_users}     = $users{local}{invisible_users};
    $users{global}{non_invisible_users} = $users{local}{non_invisible_users};
#   $users{global}{servers}             = $users{local}{servers};
    $users{global}{operators}           = $users{local}{operators};
    $users{global}{channels}            = $users{local}{channels};
}

sub process_command {
    my ($fh, $args) = @_;
    if    ($args =~ m/^USER /) { serv_user($fh, $args) }
    elsif ($args =~ m/^NICK /) { serv_nick($fh, $args) }
    elsif ($args =~ m/^PRIVMSG /) { serv_privmsg($fh, $args) }
    else {
        my $command = (split /\s+/, $args)[0];
        $command =~ tr/A-Z/a-z/;
        send_user_msg_num($fh, '421', "$command :Unknown command or command not yet implemented")
    }
}

sub serv_user {
    my ($fh, $args) = @_;
    if (!$users{$fh}{connected}) {
        my @user_info = split /\s+/, $args;
        if (@user_info < 5) { send_user_msg_num($fh, '461', "USER :Not enough parameters") }
        else {
            $users{$fh}{user_name} = '~' . $user_info[1];
            $user_info[4] =~ s/://;
            $users{$fh}{real_name} = $user_info[4];
 
           if ($users{$fh}{nick}) {
                $users{$fh}{connected} = 1;
                new_logon($fh);
            }
        }
    }
    else { send_user_msg_num($fh, '462', ":You may not reregister") }
}

sub serv_nick {
    my ($fh, $args) = @_;
    if (!$users{$fh}{connected}) {
        my $nick = (split /\s+/, $args)[1];
        $nick =~ s/://;
        if (!$nick) {
            send_user_msg_num($fh, '431', ":No nickname given");
            return;
        }
        
        elsif ($users{local}{$nick}) {
            send_user_msg_num($fh, '433', "$nick :Nickname is already in use.");
            return;
        }
        
        else {
            $users{$fh}{nick} = $nick;
            $users{local}{$nick} = 1;
            $users{local}{fh}{$nick} = $fh;
        }

        if ($users{$fh}{user_name}) {
            $users{$fh}{connected} = 1;
            new_logon($fh);
        }
    }
    else {
        my $nick = (split /\s+/, $args)[1];
        $nick =~ s/://;
        if (!$nick) {
            send_user_msg_num($fh, '431', ":No nickname given");
            return;
        }

        elsif ($nick eq $users{$fh}{nick}) { return }

        elsif ($users{local}{$nick}) {
            send_user_msg_num($fh, '433', "$nick :Nickname is already in use.");
            return;
        }
        
        else {
            send_user_msg_addr($fh, "NICK :$nick");
            $users{local}{$users{$fh}{nick}} = 0;
            $users{local}{$nick} = 1;
            $users{local}{fh}{$nick} = $fh;
            $users{$fh}{nick} = $nick;
        }
    }
}

sub serv_privmsg {
    my ($fh, $args) = @_;
    my $nick = (split /\s+/, $args)[1];
    if (!$users{local}{$nick}) {
        send_user_msg_num($fh, '401', "$nick :No such nick");
        return;
    }
    else {
        my $send_to_fh = $users{local}{fh}{$nick};
        send_user_msg_raw($send_to_fh, ":"."$users{$fh}{nick}"."!"."$users{$fh}{user_name}"."@"."$users{$fh}{real_host_name} $args");
    }
}

sub disconnect_dead_clients {
    foreach my $fh (@clients_to_disconnect) {
        shift @clients_to_disconnect;
        print "Client disconnected: $users{$fh}{real_host_name}\n";
        $serv_readable->remove($fh);
        $serv_writable->remove($fh);
        delete $users{$fh};
        $fh->close;
    }
}
